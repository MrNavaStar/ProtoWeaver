plugins {
    id 'fabric-loom' version '1.4.4'
}

def targetJavaVersion = project.javaVersion as Integer
version = project.mod_version
group = project.maven_group

repositories {
    maven { url "https://api.modrinth.com/maven" }
    maven { url = 'https://maven.minecraftforge.net' }
}

dependencies {
    shadow implementation(project(path: ':core', configuration: 'shadow')) {
        exclude(group: "org.apache.logging.log4j")
    }
    shadow implementation("org.bouncycastle:bcpkix-jdk18on:${project.bouncy_version}")

    // Fabric
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    //Forge
    compileOnly "net.minecraftforge:javafmllanguage:${project.forge_version}"
    compileOnly "net.minecraftforge:fmlloader:${project.forge_version}"

    // Dev Env
    modImplementation("maven.modrinth:easy-eula:1.0.0")
    modImplementation("maven.modrinth:fabricproxy-lite:v2.7.0")
    modImplementation "com.moandjiezana.toml:toml4j:0.7.2"
}

shadowJar {
    configurations = [project.configurations.shadow]
}

jar.finalizedBy(shadowJar)

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}