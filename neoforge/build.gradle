plugins {
    id 'net.neoforged.moddev' version '2.0.134'
}

neoForge {
    version = project.neoforge_version

    runs {
        server {
            server()
            programArgument '--nogui'
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }
}

configurations {
    zip.exclude group: "net.fabricmc", module: "fabric-loader"
}

dependencies {
    zip compileOnly(project(path: ':loader-common', configuration: 'default'))

    // Libs
    jarJar implementation("org.apache.fory:fory-core:${project.fory_version}")
    jarJar implementation("org.bouncycastle:bcpkix-jdk18on:${project.bouncy_version}")
    jarJar implementation("org.bouncycastle:bcprov-jdk18on:${project.bouncy_version}")
    jarJar implementation("org.bouncycastle:bcutil-jdk18on:${project.bouncy_version}")
    jarJar implementation("io.netty:netty-codec-http2:${project.netty_version}")
    jarJar implementation("me.mrnavastar:r:${project.r_version}")

    // Mod Compat
    compileOnly "maven.modrinth:proxy-compatible-forge:${project.proxy_forge_version}"
}

jar {
    archiveFileName = "protoweaver-${version}-neoforge.jar"
    from {
        exclude "architectury.common.marker"
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.zip.collect { it.isDirectory() ? it : zipTree(it) }
    }
}