plugins {
    id "com.gradleup.shadow" version "9.3.1"
    id "io.freefair.lombok" version "9.2.0"
    id "maven-publish"
}

allprojects {
    version = project.project_version
    group = project.maven_group

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.encoding = "UTF-8"
            options.release = project.javaVersion as Integer
        }
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "io.freefair.lombok"

    dependencies {
        compileOnly "org.apache.fory:fory-core:${project.fory_version}"
        compileOnly "io.netty:netty-buffer:${project.netty_version}"
        compileOnly "io.netty:netty-transport:${project.netty_version}"
        compileOnly "io.netty:netty-handler:${project.netty_version}"
        compileOnly "org.bouncycastle:bcpkix-jdk18on:${project.bouncy_version}"
        compileOnly "me.mrnavastar:r:${project.r_version}"

        compileOnly "org.projectlombok:lombok:${project.lombok_version}"
	    annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
    }

    tasks.register('jarSources', Jar) {
        archiveBaseName.set(project.name)
        archiveClassifier.set("sources")
        exclude {
            sourceSets.main.allSource.contains it.file
        }
        from delombok
    }

    jar.finalizedBy(jarSources)

    publishing {
        repositories {
            maven {
                url "https://maven.mrnavastar.me/releases"
                credentials {
                    username = System.getenv("MAVEN_NAME")
                    password = System.getenv("MAVEN_SECRET")
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                groupId = "me.mrnavastar.protoweaver"
                artifactId = project.name
                from components.java
                artifact tasks.jarSources
            }
        }
    }
}
