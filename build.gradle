plugins {
    id "io.github.goooler.shadow" version "8.1.8"
    id "io.freefair.lombok" version "8.12.1"
    id "architectury-plugin" version "3.4.161"
    id "dev.architectury.loom" version "1.9-SNAPSHOT" apply false
    id "maven-publish"
}

allprojects {
    apply plugin: "architectury-plugin"

    version = project.project_version
    group = project.maven_group

    architectury {
        compileOnly()
    }

    repositories {
        mavenCentral()
        maven { url "https://maven.minecraftforge.net/" }                   // Forge
        maven { url "https://repo.papermc.io/repository/maven-public/" }    // Paper + Velocity
        maven { url "https://api.modrinth.com/maven/" }                     // Modrinth
    }
    
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = project.javaVersion as Integer
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "io.freefair.lombok"

    dependencies {
        compileOnly "org.apache.fury:fury-core:${project.fury_version}"
        compileOnly "io.netty:netty-buffer:${project.netty_version}"
        compileOnly "io.netty:netty-transport:${project.netty_version}"
        compileOnly "io.netty:netty-handler:${project.netty_version}"
        compileOnly "io.netty:netty-codec-http:${project.netty_version}"
        compileOnly "io.netty:netty-codec-http2:${project.netty_version}"
        compileOnly "org.bouncycastle:bcpkix-jdk18on:${project.bouncy_version}"
        compileOnly "me.mrnavastar:r:${project.r_version}"

        compileOnly "org.projectlombok:lombok:${project.lombok_version}"
	    annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
    }

    task jarSources(type:Jar){
        archiveBaseName.set("protoweaver")
        archiveClassifier.set("sources")
        exclude {
            sourceSets.main.allSource.contains it.file
        }
        from delombok
    }

    jar.finalizedBy(jarSources)

    if (project.path != ':loader-common') {
        publishing {
            repositories {
                maven {
                    url "https://maven.mrnavastar.me/releases"
                    credentials {
                        username = System.getenv("MAVEN_NAME")
                        password = System.getenv("MAVEN_SECRET")
                    }
                }
            }
            publications {
                mavenJava(MavenPublication) {
                    groupId = "me.mrnavastar.protoweaver"
                    artifactId = project.name
                    from components.java
                    artifact tasks.jarSources
                }
            }
        }
    }
}
