plugins {
    id "io.github.goooler.shadow" version "8.1.7"
    id "io.freefair.lombok" version "8.6"
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.6.396" apply false
    id "maven-publish"
}

allprojects {
    apply plugin: "io.freefair.lombok"
    apply plugin: "architectury-plugin"

    architectury {
        compileOnly()
    }

    version = project.project_version
    group = project.maven_group

    repositories {
        mavenCentral()
        maven { url "https://maven.minecraftforge.net" }                    // Forge
        maven { url "https://repo.papermc.io/repository/maven-public/" }    // Paper + Velocity
        maven { url "https://api.modrinth.com/maven" }                      // Modrinth
    }
    
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = project.javaVersion as Integer
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"

    version = project.project_version
    group = project.maven_group

    dependencies {
        compileOnly "com.esotericsoftware:kryo5:${project.kyro_version}"
        compileOnly "io.netty:netty-buffer:${project.netty_version}"
        compileOnly "io.netty:netty-transport:${project.netty_version}"
        compileOnly "io.netty:netty-handler:${project.netty_version}"
        compileOnly "io.netty:netty-codec-http:${project.netty_version}"
        compileOnly "io.netty:netty-codec-http2:${project.netty_version}"
        compileOnly "org.bouncycastle:bcpkix-jdk18on:${project.bouncy_version}"

        compileOnly "org.projectlombok:lombok:${project.lombok_version}"
	    annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
    }

    jar {
        archiveBaseName.set("protoweaver")
        archiveClassifier.set(project.name)
    }

    task jarSources(type:Jar){
        archiveBaseName.set("protoweaver")
        archiveClassifier.set(project.name + "-sources")
        exclude {
            sourceSets.main.allSource.contains it.file
        }
        from delombok
    }

    jar.finalizedBy(jarSources)

    publishing {
        repositories {
            maven {
                name "MrNavaStar"
                url "https://maven.mrnavastar.rocks/snapshots"
                credentials {
                    username = System.getenv("MAVEN_NAME")
                    password = System.getenv("MAVEN_SECRET")

                }
            }
        }
        publications {
            maven(MavenPublication) {
                groupId = "me.mrnavastar"
                artifactId = "protoweaver"
                from components.java
                artifact tasks.jarSources
            }
        }
    }
}